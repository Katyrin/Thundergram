plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.katyrin.thundergram"
        minSdk 21
        targetSdk 31
        versionCode 4
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("apikey.properties").newDataInputStream())

            storeFile file(properties['store_file'])
            storePassword properties['store_password']
            keyAlias properties['key_alias']
            keyPassword properties['key_password']
        }

        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("apikey.properties").newDataInputStream())

            storeFile file(properties['store_file'])
            storePassword properties['store_password']
            keyAlias properties['key_alias']
            keyPassword properties['key_password']
        }
    }

    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("apikey.properties").newDataInputStream())
        def appId = properties.getProperty("app_id", "")
        it.buildConfigField "int", "APP_ID", appId
        def appHash = properties.getProperty("app_hash", "")
        it.buildConfigField "String", "APP_HASH", appHash
        def adUnitId = properties.getProperty("ad_unit_id", "")
        it.buildConfigField "String", "AD_UNIT_ID", adUnitId
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTestData/java"
        }
        test {
            java.srcDirs += "src/sharedTestData/java"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    flavorDimensions "default"
    productFlavors {
        fake {
            applicationIdSuffix ".fake"
            versionNameSuffix "-fake"
            buildConfigField "String", "TYPE", '"FAKE"'
        }
        real {
            applicationIdSuffix ".real"
            versionNameSuffix "-real"
            buildConfigField "String", "TYPE", '"REAL"'
        }
    }
}

dependencies {
    implementation project(':libtd-ktx')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    //Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    //Dagger
    implementation 'com.google.dagger:dagger:2.39.1'
    implementation 'com.google.dagger:dagger-android:2.39.1'
    implementation 'com.google.dagger:dagger-android-support:2.39.1'
    kapt 'com.google.dagger:dagger-compiler:2.39.1'
    kapt 'com.google.dagger:dagger-android-processor:2.39.1'

    //Telegram
    implementation 'com.github.tdlibx:td:1.6.0'

    //Glide
    implementation "com.github.bumptech.glide:glide:4.12.0"

    //ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer:2.16.1'

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-database-ktx:20.0.3'

    //Billing ADS
    implementation 'com.google.android.gms:play-services-ads:20.5.0'
    implementation "com.android.billingclient:billing-ktx:4.0.0"

    //Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Coroutine Testing
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"
    testImplementation 'org.mockito:mockito-inline:3.8.0'
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.robolectric:robolectric:4.6.1'

    //Test Kaspresso
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:core-ktx:1.4.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.3.0'

    //UI Automator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}